
//Generate multiple kimographs for domain analysis.
macro "Kymograph"{
	
	setBatchMode(true);
	//Image ID to be analyzed.
	id1=getImageID();
	
	//New images are generated by the number of registered ROI.
	n = roiManager("count");
	newImage("Kymograph","16-bit",180,nSlices,n);//The left half of the image shows the fluorescence of the cell membrane and the right half shows the fluorescence of the cytoplasm.
	
	selectImage(id1);//Switch from the kimograph to the image to be analyzed.
	
	//Register the ROI in the stack direction.
	for (i=0; i<n; i++) {
		roiManager("select", 0);	
		for (ii=1; ii<=nSlices; ii++) {
			setSlice(ii);
			run("Add to Manager");
		}
		roiManager("Delete");
	}
	
	
	for (i=0; i<n; i++) {
		//Switch the analysis target.
		//Select ROI one by one.
		selectImage(id1);
		roiManager("select", i*nSlices);//Move to the first ROI of the next target.
		
		//Get the size of ROI.
		Roi.getBounds(x,y,w,h);
		r=w/2;
		r1=(r-1)/r;
		r2=(r+1)/r;
		r3=(r-2)/r;
		r4=(r+2)/r;
		r5=(r-3)/r;
		r6=(r+3)/r;
		
		xc=x+w/2;
		yc=y+w/2;
		
  		for (l=0; l<nSlices; l++) {
			//Advance in stack direction.
	        roiManager("select", i*nSlices+l);
	        getStatistics(area,mean);//Acquisition of average fluorescence intensity of cytoplasm. The first argument is area and the second argument is average fluorescence intensity.
	
	        for (j=0; j<90; j++){
		        //It is optimal for resolution to divide the circle by 90 degrees.
		        
		        selectImage(id1);//Go to the analysis image.
		        
		        X0=r*cos(2*PI*(j*4+1)/360)+xc;
		        Y0=r*sin(2*PI*(j*4+1)/360)+yc;     
		        X1=xc+(X0-xc)*r1;
		        Y1=yc+(Y0-yc)*r1;      
		        X2=xc+(X0-xc)*r2;
		        Y2=yc+(Y0-yc)*r2;
		        X3=xc+(X0-xc)*r3;
		        Y3=yc+(Y0-yc)*r3;      
		        X4=xc+(X0-xc)*r4;
		        Y4=yc+(Y0-yc)*r4;
		        X5=xc+(X0-xc)*r5;
		        Y5=yc+(Y0-yc)*r5;      
		        X6=xc+(X0-xc)*r6;
		        Y6=yc+(Y0-yc)*r6;
		
		        v0=getPixel(X0,Y0); 
		        v1=getPixel(X1,Y1);	
		        v2=getPixel(X2,Y2);
		        v3=getPixel(X3,Y3);
		        v4=getPixel(X4,Y4);
		        v5=getPixel(X5,Y5);
		        v6=getPixel(X6,Y6);
		     
		
		        //Acquire the maximum value of the fluorescence intensity.
		        v=newArray(v0,v1,v2,v3,v4,v5,v6);
		        Array.sort(v);
		        max=v[6];
		
		        selectImage("Kymograph");//Go to Kymograph image.
		        Stack.setSlice(i+1);
		       	setPixel(j,l,max);
		
		        for(p=0;p<=90;p++){
		        	setPixel(90+p,l,mean);
		        }	        
		        selectImage(id1);		        
        	}
  		}
        selectImage("Kymograph");
        Stack.setSlice(i+1);
        run("Enhance Contrast", "saturated=0.35");//Contrast adjustment.
	}
	setBatchMode(false);
}

